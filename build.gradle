def loadProperties(String sourceFileName) {
    def config = new Properties()
    def propFile = new File(sourceFileName)
    System.err.println("Loading property file: " + propFile.absolutePath)
    if (propFile.canRead()) {
        config.load(new FileInputStream(propFile))
        for (Map.Entry property in config) {
            System.out.println("setting " + property.key)
            project.ext[property.key] = property.value
        }
    }
}

loadProperties("local.properties")

buildscript {
    ext.kotlin_version = '1.3.40'
    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'https://oss.sonatype.org/service/local/repositories/releases/content/' }
        google()
        mavenCentral()
        jcenter()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'https://oss.sonatype.org/service/local/repositories/releases/content/' }
        google()
        mavenCentral()
        jcenter()
        maven { url "https://jitpack.io" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    apply plugin: 'com.android.library'
    android {
        compileSdkVersion 29
        buildToolsVersion "29.0.0"

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        defaultConfig {
            minSdkVersion 21
            targetSdkVersion 29

            versionCode 200
            versionName "${versionCode}"

            multiDexEnabled true
        }

        lintOptions {
            abortOnError false
            disable "ResourceType"
            warning 'NewApi', 'OnClick', 'EllipsizeMaxLines'
        }

        buildTypes {
            release {
                minifyEnabled false
                shrinkResources false
            }
            debug {
                minifyEnabled false
                shrinkResources false
            }
        }
    }

}

subprojects {
    apply plugin: 'maven'

    uploadArchives {
        repositories.mavenDeployer {
            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.artifactId = project.name
            pom.version = '2.0.0'
            pom.groupId = "com.rover12421.AndroidHideApi"
        }
    }

}