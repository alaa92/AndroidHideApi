apply plugin: 'idea'

buildscript {
    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'https://oss.sonatype.org/service/local/repositories/releases/content/' }
        maven { url 'https://jitpack.io' }
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

idea {
    project {
        languageLevel = jdkVer
    }
}

def loadProperties(String sourceFileName) {
    def config = new Properties()
    def propFile = new File(sourceFileName)
    System.err.println("Loading property file: " + propFile.absolutePath)
    if (propFile.canRead()) {
        config.load(new FileInputStream(propFile))
        for (Map.Entry property in config) {
            System.out.println("setting " + property.key)
            project.ext[property.key] = property.value
        }
    }
}

loadProperties("local.properties")

allprojects {
    group 'com.rover12421.AndroidHideApi'

    // used for official releases only. Please don't use
    task release {
    }

    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'https://oss.sonatype.org/service/local/repositories/releases/content/' }
        maven { url 'https://jitpack.io' }
        google()
        mavenCentral()
        jcenter()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'

    configurations {
        provided
    }

    sourceCompatibility = jdkVer
    targetCompatibility = jdkVer

    sourceSets {
        main {
            compileClasspath += configurations.provided
            resources {
                srcDirs = ["src/main/java"]
                includes = ["**/*.aidl"]
            }
        }
    }

    tasks.withType(Javadoc) {
        classpath += configurations.provided
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
        options.addStringOption('charSet', 'UTF-8')
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

}

configure(subprojects - project(':xposed')) {
    def ANDROID_HOME = System.getenv()['ANDROID_HOME']

    dependencies {
        provided files("$ANDROID_HOME/platforms/android-28/android.jar")
    }
}

configure(subprojects - project(':test')) {
    artifacts {
        archives javadocJar
        archives sourcesJar
    }

    signing {
        required { gradle.taskGraph.hasTask('uploadArchives') }
        sign configurations.archives
    }

    uploadArchives {
        repositories.mavenDeployer {
            configuration = configurations.archives

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.artifactId = project.name

            pom.project {
                name project.name
                description "Android Hidden API / $project.name"
                url 'http://www.rover12421.com'
                packaging 'jar'
                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/mit-license.php'
                        distribution 'repo'
                    }
                }
                scm {
                    url 'https://github.com/rover12421/AndroidHideApi/tree/master'
                    connection 'scm:git:git://github.com/rover12421/AndroidHideApi.git'
                    developerConnection 'scm:git:git@github.com:rover12421/AndroidHideApi.git'
                }
                developers {
                    developer {
                        id 'rover12421'
                        name 'Rover12421'
                        email 'rover12421@163.com'
                    }
                }
            }
        }
    }

    tasks.getByPath(":${project.name}:release").dependsOn(uploadArchives)
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2'
}
